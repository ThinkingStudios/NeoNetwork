plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT'
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
    id 'maven-publish'
}

group = project.maven_group
version = "$project.mod_version+mc$project.minecraft_version"
base.archivesName = project.archives_name

repositories {
    // NeoForged repository.
    maven {
        name = "NeoForged"
        url = "https://maven.neoforged.net/releases"
    }
}

loom {
    silentMojangMappingsLicense()
}

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings loom.officialMojangMappings()
    neoForge "net.neoforged:neoforge:$project.neoforge_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

publisher {
    apiKeys {
        modrinth System.getenv("MODRINTH_TOKEN")
        curseforge System.getenv("CURSEFORGE_TOKEN")
    }

    setCurseID("${project.curseforge_id}")
    setModrinthID("${project.modrinth_id}")
    setVersionType("alpha")
    setChangelog(file("CHANGELOG.md").getText("UTF-8"))
    setVersion("${project.version}")
    setDisplayName("${project.version}")
    setGameVersions("1.21")
    setLoaders("neoforge")
    setCurseEnvironment("both")
    setArtifact(remapJar)
    addAdditionalFile(sourcesJar)
}